#!/usr/bin/env perl

#*  Copyright (c) 2017-2026 Frederic Jardon  <frederic.jardon@gmail.com>
#*
#*  ------------------ GPL Licensed Source Code ------------------
#*  Frederic Jardon makes this software available under the GNU
#*  General Public License (GPL) license for open source projects.
#*  For details of the GPL license please see www.gnu.org or read
#*  the file license.gpl provided in this package.
#*
#*  This program is free software; you can redistribute it and/or
#*  modify it under the terms of the GNU General Public License as
#*  published by the Free Software Foundation; using version 3 of 
#*  the License.
#*
#*  This program is distributed in the hope that it will be useful,
#*  but WITHOUT ANY WARRANTY; without even the implied warranty of
#*  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#*  GNU General Public License for more details.
#*
#*  You should have received a copy of the GNU General Public
#*  License along with this program in the file 'license.gpl'; if
#*  not, see <http://www.gnu.org/licenses/>.
#*  --------------------------------------------------------------

use strict;
use warnings 'all';

require Carp;
require Cwd;
require File::Basename;
require Getopt::Long;
require Storable;

use File::Temp;

sub call_vcvarsall {
    my ($opts) = @_;

    # Save current context to restore it later
    my %saved_ENV = (%ENV);
    my $cwd = Cwd::getcwd;

    # Get environment
    my $env = $opts->{ENV};
    $env //= \%ENV;

    # Get path to comspec
    my $comspec_win=$env->{'COMSPEC'};
    Carp::croak("Unable to find 'COMSPEC' in the environment!")
        if(!defined($comspec_win));
    my $comspec_path = Cygwin::win_to_posix_path($comspec_win);

    # Get path to vcvarsall.bat
    my $vcvarsall_bat_path = $opts->{vcvarsall_bat_path};
    Carp::croak("'vcvarsall_bat_path' parameter is mandatory")
        if(!defined($vcvarsall_bat_path));

    # Get vcvarsall.bat arguments
    my $vcvarsall_args = $opts->{args};
    $vcvarsall_args  //= [];

    # Get paths to scripts and bash
    my $bat_dir      = File::Basename::dirname($vcvarsall_bat_path);
    my $bat_dir_win  = Cygwin::posix_to_win_path($bat_dir);
    my $bat_path_win = Cygwin::posix_to_win_path($vcvarsall_bat_path);
    my $bash_dir     ='/usr/bin';
    my $bash_dir_win = Cygwin::posix_to_win_path($bash_dir);

    # Prepare a temporary file to store environment
    my $tmp_env_handle   = File::Temp->new();
    my $tmp_env_filename = "$tmp_env_handle";
    # Close the temporary file handle to avoid 'text file is busy' errors
    close($tmp_env_handle);

    # Write .bat to a temporary file
    my $tmp_bat_handle = File::Temp->new(SUFFIX => '.bat');
    my $tmp_bat        = "$tmp_bat_handle";
    my $tmp_bat_win    = Cygwin::posix_to_win_path($tmp_bat);
    my $tmp_bat_dir    = File::Basename::dirname($tmp_bat);
    # Close the temporary file handle to avoid 'text file is busy' errors
    close($tmp_bat_handle);

    # Compute parameters
    my $vcvarsall_params = join(' ', @{$vcvarsall_args});

    open(my $fh, '>', $tmp_bat) or
        Carp::croak("Unable to create batch file");
    print $fh <<BATCH;
\@ECHO OFF
CD "$bat_dir_win"
CALL "${bat_path_win}" $vcvarsall_params
CD "$bash_dir_win"
bash -c "perl -MStorable -e 'Cygwin::sync_winenv();' -e 'Storable::store \\%%ENV, \\\"$tmp_env_filename\\\";'"
BATCH
    chmod 0755, $tmp_bat;
    chdir $tmp_bat_dir;
    print "Executing: '${tmp_bat}' or '${tmp_bat_win}'\n";
    # Add MSYS2_ARG_CONV_EXCL=* to the environment to prevent the '/C'
    # argument to be converted to 'C:\' by msys2.
    $ENV{'MSYS2_ARG_CONV_EXCL'}='*';
    system $comspec_path, '/C', $tmp_bat_win;

    # Read modified environment
    my %vc_env = %{Storable::retrieve($tmp_env_filename)};

    # Check for errors
    Carp::croak("Error during setup of VC++ environment")
        if(! exists($vc_env{VSCMD_VER}) || $vc_env{VSCMD_VER} eq '');

    # Start subshell with modified environment
    %ENV = %vc_env;
    # Restore the original value of MSYS2_ARG_CONV_EXCL
    if (exists($saved_ENV{'MSYS2_ARG_CONV_EXCL'})) {
        $ENV{'MSYS2_ARG_CONV_EXCL'}=$saved_ENV{'MSYS2_ARG_CONV_EXCL'};
    }
    else {
        delete $ENV{'MSYS2_ARG_CONV_EXCL'};
    }
    $ENV{PWD} = $cwd;
    $ENV{OLDPWD} = $cwd;
    $ENV{VS_KIT}=join('-', 'msvc', $vc_env{VSCMD_VER}, @{$vcvarsall_args});
    chdir $cwd;
    system 'bash', '-i';

    # Restore saved environment
    chdir $cwd;
    %ENV = %saved_ENV;
}

sub get_default_vs_paths {
    my @default_vs_paths = ();
    push(@default_vs_paths, $ENV{'ProgramW6432'})
        if(exists($ENV{'ProgramW6432'}));
    push(@default_vs_paths, $ENV{'ProgramFiles(x86)'})
        if(exists($ENV{'ProgramFiles(x86)'}));
    @default_vs_paths = map { Cygwin::win_to_posix_path($_) } @default_vs_paths;
    @default_vs_paths = map { $_.'/Microsoft Visual Studio' } @default_vs_paths;
    @default_vs_paths = map { my $year=$_; map { $_.'/'.$year } @default_vs_paths; } (18, 2022, 2019, 2017);
    @default_vs_paths = map { $_.'/Community', $_.'/Professional', $_.'/Enterprise' } @default_vs_paths;
    @default_vs_paths = map { $_.'/VC/Auxiliary/Build/vcvarsall.bat' } @default_vs_paths;
    return @default_vs_paths;
}

# Load the full windows environment
Cygwin::sync_winenv();

# Parse options
my ($opt_help, $opt_p);
Getopt::Long::Configure("no_ignore_case");
Getopt::Long::GetOptionsFromArray(
    \@ARGV,
    'help|h'             => \$opt_help,
    'p|vcvarsall-path=s' => \$opt_p,
) or Carp::croak('Error parsing command line arguments');

# Handle help option
if($opt_help) {
    require Pod::Usage;
    Pod::Usage::pod2usage(-exitval => 0);
}

# Clean '-p' option
$opt_p = Cygwin::win_to_posix_path($opt_p)
    if(defined($opt_p));
if(! defined($opt_p)) {
    my @default_vs_paths = get_default_vs_paths();
    my @found_vcvars = grep { -f $_ } @default_vs_paths;
    if(@found_vcvars) {
        $opt_p = $found_vcvars[0];
        print "Using 'vcvarsall.bat' found in path: ".File::Basename::dirname($opt_p)."\n";
    }
}
if(! defined($opt_p)) {
    print STDERR "Unable to find a suitable path to 'vcvarsall.bat'. Please use option '-p'\n";
    require Pod::Usage;
    Pod::Usage::pod2usage(-exitval => 1);
}
if(! -f $opt_p) {
    print STDERR "The path specified by option '-p' is not valid.\n";
    require Pod::Usage;
    Pod::Usage::pod2usage(-exitval => 1);
}

call_vcvarsall({
    vcvarsall_bat_path => $opt_p,
    args => \@ARGV,
});


__END__
=head1 NAME

msvc-shell - MS-VC++ build environment shell spawner

=head1 SYNOPSIS

B<msvc-shell> B<-h>|B<--help>

B<msvc-shell> [B<OPTIONS>] B<VC_OPTIONS>

=head1 DESCRIPTION

This tool wraps the B<vcvarsall.bat> batch script provided with visual studio
to setup the build environment. Calling this script allows to spawn a shell
with the same configuration that B<vcvarsall.bat> setup when called in a
windows console.

The tool also setup a B<VS_KIT> environment variable in the spawned shell to
indicate the parameters that were passed to B<vcvarsall.bat>.

=head1 OPTIONS

=over

=item B<-h>|B<--help>

Print the usage, help and version information for this program and exit.

=item B<-p> I<path/to/vcvarsall.bat>|B<--vcvarsall-path>=I<path/to/vcvarsall.bat>

Sets the path to the B<vcvarsall.bat> batch script. Before Visual Studio 2017
it was possible to deduce the path to this script from the environment variable
set at install time, but this is no longer the case.

=back

=head1 VC_OPTIONS

The B<vcvarsall.bat> script accepts parameters to indicate which architecture,
platform, SDK, etc. is targeted by the environment it sets up.

Run the script without B<VC_OPTIONS> to get more information in the error
message printed by B<vcvarsall.bat>.

=head1 SEE ALSO


=head1 AUTHOR

Frederic JARDON <frederic.jardon@gmail.com>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2017-2026 by Frederic JARDON <frederic.jardon@gmail.com>

This program is free software; you can redistribute it and/or modify
it under the GPL license.

=cut

